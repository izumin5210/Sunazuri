apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'

apply plugin: 'kotlin-android'

def esaParams = [
        apiEndpoint:    esa_api_endpoint,
        oauthScope:     esa_oauth_scope,
        oauthGrantType: esa_oauth_grant_type,
        clientId:       System.getenv("ESA_CLIENT_ID") ?: esa_client_id,
        clientSecret:   System.getenv("ESA_CLIENT_SECRET") ?: esa_client_secret,
        callbackScheme: System.getenv("ESA_CALLBACK_SCHEME") ?: esa_callback_scheme,
        callbackHost:   System.getenv("ESA_CALLBACK_HOST") ?: esa_callback_host
]

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "info.izumin.android.sunazuri"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "ESA_API_ENDPOINT",      "\"${esaParams.apiEndpoint}\""
        buildConfigField "String", "ESA_OAUTH_SCOPE",       "\"${esaParams.oauthScope}\""
        buildConfigField "String", "ESA_OAUTH_GRANT_TYPE",  "\"${esaParams.oauthGrantType}\""
        buildConfigField "String", "ESA_CLIENT_ID",         "\"${esaParams.clientId}\""
        buildConfigField "String", "ESA_CLIENT_SECRET",     "\"${esaParams.clientSecret}\""
        buildConfigField "String", "ESA_CALLBACK_URI",      "\"${esaParams.callbackScheme}://${esaParams.callbackHost}\""

        manifestPlaceholders = [
                esaCallbackScheme: esaParams.callbackScheme,
                esaCallbackHost: esaParams.callbackHost
        ]

        buildConfigField "String", "KEYSTORE_ALIAS", "\"${keystore_alias}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations.all {
  resolutionStrategy {
    force "com.android.support:support-annotations:$support_lib_version"
    force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  }
}

kapt {
    generateStubs = true
}

dependencies {
    compile "com.android.support:support-v4:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:preference-v7:${support_lib_version}"
    compile "com.android.support:recyclerview-v7:${support_lib_version}"
    compile "com.android.support:support-vector-drawable:${support_lib_version}"
    compile "com.android.support:customtabs:${support_lib_version}"
    compile "com.android.support:design:${support_lib_version}"
    compile "com.android.support:percent:${support_lib_version}"

    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.eccyan:rxjava-optional:1.1.0'

    kapt 'com.google.guava:guava:19.0'
    kapt 'com.android.databinding:compiler:2.1.0'

    compile 'com.google.dagger:dagger:2.4'
    kapt 'com.google.dagger:dagger-compiler:2.4'
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'info.izumin.android:droidux:0.6.0'
    kapt 'info.izumin.android:droidux-processor:0.6.0'
    compile 'info.izumin.android:droidux-thunk:0.3.0'

    compile 'com.github.gfx.android.orma:orma:2.4.5'
    kapt 'com.github.gfx.android.orma:orma-processor:2.4.5'

    compile "com.google.code.gson:gson:2.6.2"
    compile 'com.github.gfx.static_gson:static-gson:0.9.7'
    kapt 'com.github.gfx.static_gson:static-gson-processor:0.9.7'

    compile 'com.rejasupotaro:kvs-schema:5.0.0'
    kapt 'com.rejasupotaro:kvs-schema-compiler:5.0.0'

    compile 'com.vanniktech:onactivityresult:0.4.0'
    kapt 'com.vanniktech:onactivityresult-compiler:0.4.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'

    compile 'com.facebook.conceal:conceal:1.1.2@aar'

    compile 'com.jakewharton.timber:timber:4.1.2'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.github.magiepooh:recycler-itemdecoration:1.1.1@aar'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"


    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'


    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    testCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'

    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    testCompile 'com.squareup.retrofit2:retrofit-mock:2.0.2'
}
